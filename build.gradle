import java.text.SimpleDateFormat

buildscript {
    apply from: "$rootDir/dependencies.gradle"
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath(gradlePlugins.kotlin)
    }
}

static def buildTimeStamp() {
    return new SimpleDateFormat("yyyyMMdd-HHmmss").format(new Date())
}

static def gitSha() {
    try {
        return "git rev-parse --short HEAD".execute().text.trim()
    } catch (ignored) {
        return "unknown"
    }
}

allprojects {
    defaultTasks "clean", "build", "jacocoTestReport"
}

subprojects {

    apply from: "$rootDir/dependencies.gradle"
    apply plugin: "kotlin"
    apply plugin: "jacoco"

    group = "org.suggs.aoc"

    repositories {
        mavenCentral()
    }

    jar {
        archiveVersion = "${buildTimeStamp()}-${gitSha()}"
    }

    dependencies {
        implementation(libs.kotlin,
                libs.kotlinReflect,
                libs.slf4j,
                libs.coroutines)

        testImplementation(libs.test.kotlinTest,
                libs.test.coroutines)
    }

    ext {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    testing {
        suites {
            test {
                useJUnitJupiter(junitVersion)

//                systemProperty("junit.jupiter.execution.parallel.enabled", "true")
//                systemProperty("junit.jupiter.execution.parallel.mode.default", "concurrent")

                dependencies {
                    implementation libs.test.junitApi
                    implementation libs.test.kotestAssertions
                    implementation libs.test.mockito
                    runtimeOnly libs.test.junitEngine
                    runtimeOnly libs.logback
                }
            }
        }
    }
}
